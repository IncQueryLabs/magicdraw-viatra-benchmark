package com.incquerylabs.magicdraw.benchmark.queries

import "http://www.nomagic.com/magicdraw/UML/2.5.1"

pattern participantPropertyDifferentType(property : Property) {
	find sysml.ParticipantProperty(property, _);
	Property.type(property, type);
	find sysml.ParticipantProperty_end(property, propertyEnd);
	neg Property.type(propertyEnd, type);
}

pattern refineNotSingleClient(abstraction : Abstraction) {
	find sysml.Refine(abstraction, _);
	clients == count Abstraction.client(abstraction, _);
	check(clients != 1);
}

pattern requirementHasOperation(operation : Operation) {
	find sysml.Requirement(class, _);
	Class.ownedOperation(class, operation);
}

pattern requirementHasAttribute(attirubute : Property) {
	find sysml.Requirement(req, _);
	Class.ownedAttribute(req, attirubute);
}

pattern
requirementInAssociation(association : Association, requirement : Class) {
	Association.memberEnd(association, memberEnd);
	Property.type(memberEnd, requirement);
	find sysml.Requirement(requirement, _);
}

pattern
requirementInGeneralization(classifier : Classifier, requirement : Class) {
	Classifier.generalization(classifier, generalization);
	Generalization.general(generalization, requirement);
	find sysml.Requirement(requirement, _);
}

pattern activityEdgeDiscreteAndContinuous(activityEdge : ActivityEdge) {
	find sysml.Discrete(activityEdge, _);
	find sysml.Continuous(activityEdge, _);
}

pattern requirementAsType(element : TypedElement, requirement : Class) {
	TypedElement.type(element, requirement);
	find sysml.Requirement(requirement, _);
}

pattern flowPropertyInvalidType(property : Property) {
	find sysml.FlowProperty(property, _);
	Property.type(property, type);
	neg find sysml.Block(type, _);
	neg find sysml.ValueType(type, _);
	neg Signal(type);
}

pattern interfaceBlockIsConjugated(port : Port, class : Class) {
	find sysml.InterfaceBlock(class, _);
	Port.type(port, class);
	Port.isConjugated(port, true);
}

pattern noBufferAndOverwrite(objectNode : ObjectNode) {
	find sysml.NoBuffer(objectNode, _);
	find sysml.Overwrite(objectNode, _);
}

pattern notBinaryConnector(connector : Connector) {
	find sysml.Block(block, _);
	Class.ownedConnector(block, connector);
	ends == count Connector.end(connector, _);
	check(ends != 2);
}

pattern allocateNotSingleClient(abstraction : Abstraction) {
	find sysml.Allocate(abstraction, _);
	clients == count Abstraction.client(abstraction, _);
	check(clients != 1);
}

pattern allocateNotSingleSupplier(abstraction : Abstraction) {
	find sysml.Allocate(abstraction, _);
	suppliers == count Abstraction.supplier(abstraction, _);
	check(suppliers != 1);
}

pattern participantPropertyNotBlock(class : Class, property : Property) {
	find sysml.ParticipantProperty(property, _);
	Class.ownedAttribute(class, property);
	neg find sysml.Block(class, _);
}

pattern
participationPropertyNotAssociation(class : Class, property : Property) {
	find sysml.ParticipantProperty(property, _);
	Class.ownedAttribute(class, property);
	neg AssociationClass(class);
}

pattern participantPropertyIsMemberEndOfAssociation(property : Property) {
	find sysml.ParticipantProperty(property, _);
	Association.memberEnd(_, property);
}

pattern refineNotSingleSupplier(abstraction : Abstraction) {
	find sysml.Refine(abstraction, _);
	suppliers == count Abstraction.supplier(abstraction, _);
	check(suppliers != 1);
}

pattern participantPropertyAggregationKindNotNone(property : Property) {
	find sysml.ParticipantProperty(property, _);
	neg Property.aggregation(property, ::none);
}

pattern fullPortIsBehavioral(port : Port) {
	find sysml.FullPort(port, _);
	Port.isBehavior(port, true);
}

pattern interfaceBlockPortInvalidType(port : Port) {
	find sysml.InterfaceBlock(class, _);
	Class.ownedAttribute(class, port);
	Port.type(port, type);
	neg find sysml.InterfaceBlock(type, _);
} or {
	find sysml.InterfaceBlock(class, _);
	Class.ownedAttribute(class, port);
	neg Port.type(port, _);
}

pattern conformGeneralNotViewpoint(conform : Generalization) {
	find sysml.Conform(conform, _);
	Generalization.general(conform, general);
	neg find sysml.Viewpoint(general, _);
}

pattern conformSpecificNotView(conform : Generalization) {
	find sysml.Conform(conform, _);
	Generalization.specific(conform, specific);
	neg find sysml.View(specific, _);
}

pattern boundReferenceNotConnectorEnd(boundReference : Property) {
	find sysml.BoundReference(boundReference, _);
	neg find BoundReference_1_bindingconnector_end(boundReference);
}

private pattern
BoundReference_1_bindingconnector_end(boundReference : Property) {
	find sysml.BindingConnector(bindingConnector, _);
	Connector.end(bindingConnector, connectorEnd);
	ConnectorEnd.role(connectorEnd, role);
	Property.redefinedProperty *(boundReference, role);
}

pattern
connectionPropertyDifferentName(property : Property, connector : Connector) {
	find sysml.ConnectorProperty(property, _);
	find sysml.ConnectorProperty_connector(property, connector);
	Property.name(property, propName);
	Connector.name(connector, conName);
	propName != conName;
}

pattern
connectionPropertyNotTypedByAssociationBlock(property : Property, connector : Connector) {
	find sysml.ConnectorProperty(property, _);
	find sysml.ConnectorProperty_connector(property, connector);
	Connector.type(connector, type);
	neg find associationBlock(type);
}

private pattern associationBlock(class : AssociationClass) {
	find sysml.Block(class, _);
}

pattern connectorPropertyNotBlock(property : Property) {
	find sysml.ConnectorProperty(property, _);
	neg find owningBlock(property, _);
}

private pattern owningBlock(property : Property, class : Class) {
	Class.ownedAttribute(class, property);
	find sysml.Block(class, _);
}

pattern connectorPropertyNotComposite(property : Property) {
	find sysml.ConnectorProperty(property, _);
	neg Property.aggregation(property, ::composite);
}

pattern connectorPropertyNotOwnedOrInherited(property : Property) {
	find sysml.ConnectorProperty(property, _);
	neg find owningBlockTransitive(property, _);
}

private pattern general(specific : Classifier, general : Classifier) {
	Classifier.generalization(specific, generalization);
	Generalization.general(generalization, general);
}

private pattern owningBlockTransitive(property : Property, owner : Class) {
	find sysml.Block(owner, _);
	Class.ownedAttribute(owner, property);
	find general *(owner, general);
	find sysml.ConnectorProperty_connector(property, connector);
	Class.ownedConnector(general, connector);
}

pattern valueTypeSpecializatonNotValueType(classifier : Classifier) {
	find sysml.ValueType(valueType, _);
	find general(classifier, valueType);
	neg find sysml.ValueType(classifier, _);
}

pattern blockValuePropertyNotComposite(property : Property) {
	find sysml.Block(block, _);
	Class.ownedAttribute(block, property);
	Property.type(property, type);
	find sysml.ValueType(type, _);
	neg Property.aggregation(property, ::composite);
}

pattern valueTypeValuePropertyNotComposite(property : Property) {
	find sysml.ValueType(valueType, _);
	DataType.ownedAttribute(valueType, property);
	Property.type(property, type);
	find sysml.ValueType(type, _);
	neg Property.aggregation(property, ::composite);
}

pattern parameterDiscreteAndContinuous(parameter : Parameter) {
	find sysml.Discrete(parameter, _);
	find sysml.Continuous(parameter, _);
}

pattern probabilityInvalidStereotype(probability : NamedElement) {
	ActivityEdge(probability);
	find sysml.Probability(probability, _);
	ActivityEdge.source(probability, source);
	neg DecisionNode(source);
} or {
	ParameterSet(probability);
	find sysml.Probability(probability, _);
	ParameterSet.parameter(probability, parameter);
	neg Parameter.direction(parameter, ::^out);
}

pattern probabilityAllOutgoingEdges(activityEdge : ActivityEdge) {
	find sysml.Probability(activityEdge, _);
	ActivityEdge.target(activityEdge, target);
	ActivityNode.incoming(target, edge);
	neg find sysml.Probability(edge, _);
}

pattern rateNotStreaming(rate : Parameter) {
	find sysml.Rate(rate, _);
	Parameter.isStream(rate, false);
}

pattern copyClientNotRequirement(copy : Abstraction) {
	find sysml.Copy(copy, _);
	Abstraction.client(copy, client);
	neg find sysml.AbstractRequirement(client, _);
}

pattern copySupplierNotRequirement(copy : Abstraction) {
	find sysml.Copy(copy, _);
	Abstraction.supplier(copy, supplier);
	neg find sysml.AbstractRequirement(supplier, _);
}

pattern satisfySupplierNotRequirement(satisfy : Abstraction) {
	find sysml.Satisfy(satisfy, _);
	Abstraction.supplier(satisfy, supplier);
	neg find sysml.AbstractRequirement(supplier, _);
}

pattern stakeHolderInvalidStereotype(stakeholder : Classifier) {
	find sysml.Stakeholder(stakeholder, _);
	neg Actor(stakeholder);
	neg find classNotAssociation(stakeholder);
}

private pattern classNotAssociation(classifier : Classifier) {
	Class(classifier);
	neg Association(classifier);
}

pattern endPathMiultiplicityNotRedefiniton(endPathMultiplicity : Property) {
	find sysml.EndPathMultiplicity(endPathMultiplicity, _);
	neg Property.redefinedProperty(endPathMultiplicity, _);
} or {
	find sysml.EndPathMultiplicity(endPathMultiplicity, _);
	neg find redefinedBoundReferenceProperty(endPathMultiplicity, _);
}

private pattern
redefinedBoundReferenceProperty(property : Property, boundReference : Property) {
	Property.redefinedProperty(property, boundReference);
	find sysml.BoundReference(boundReference, _);
}

pattern endPathMiultiplicityNegative(endPathMultiplicity : Property) {
	find sysml.EndPathMultiplicity(endPathMultiplicity, _);
	Property.lowerValue(endPathMultiplicity, lower);
	LiteralInteger.value(lower, value);
	check(value < 0);
}

pattern participantPropertyEndOwner(participantProperty : Property) {
	find sysml.ParticipantProperty(participantProperty, _);
	neg find associationEnd(participantProperty);
}

private pattern associationEnd(property : Property) {
	find sysml.ParticipantProperty_end(property, end);
	Property.association(property, association);
	Association.memberEnd(association, end);
}

pattern participantPropertyMultiplicity(property : Property) {
	find sysml.ParticipantProperty(property, _);
	Property.lowerValue(property, lower);
	LiteralInteger.value(lower, value);
	check(value != 1);
} or {
	find sysml.ParticipantProperty(property, _);
	Property.upperValue(property, upper);
	LiteralUnlimitedNatural.value(upper, value);
	check(value != 1);
}

pattern propertySpecificTypeNotOneProperty(classifier : Classifier) {
	find sysml.PropertySpecificType(classifier, _);
	number == count Property.type(_, classifier);
	check(number != 1);
}

pattern adjunctPropertyCallActionInconsistent(property : Property) {
	find sysml.AdjunctProperty(property, _);
	neg find compositeAndConsistent(property);
}

private pattern compositeAndConsistent(property : Property) {
	find checkEmptiness(property);
} or {
	neg find calledBehavior(property, _);
	neg Property.type(property, _);
} or {
	find calledBehavior(property, called);
	Property.type(property, type);
	find general *(called, type);
}

private pattern checkEmptiness(property : Property) {
	find sysml.AdjunctProperty_principal(property, principal);
	CallOperationAction(principal);
	CallOperationAction.operation(principal, operation);
	neg Operation.method(operation, _);
	neg Property.type(property, _);
} or {
	find sysml.AdjunctProperty_principal(property, principal);
	CallOperationAction(principal);
	CallOperationAction.operation(principal, operation);
	Operation.method(operation, called);
	Property.type(property, type);
	find general(called, type);
}

private pattern calledBehavior(property : Property, behavior : Behavior) {
	find sysml.AdjunctProperty_principal(property, principal);
	CallBehaviorAction(principal);
	CallBehaviorAction.behavior(principal, behavior);
} or {
	find sysml.AdjunctProperty_principal(property, principal);
	find StartObjectBehaviorActionBehavior(principal, behavior);
}

private pattern
StartObjectBehaviorActionBehavior(action : StartObjectBehaviorAction, behavior : Behavior) {
	StartObjectBehaviorAction.object(action, object);
	InputPin.type(object, behavior);
} or {
	StartObjectBehaviorAction.object(action, object);
	InputPin.type(object, type);
	BehavioredClassifier(type);
	BehavioredClassifier.classifierBehavior(type, behavior);
}

pattern adjunctPropertyConnectorNotAssociationBlock(property : Property) {
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	Connector(principal);
	neg find associationBlockOfConnector(principal);
}

private pattern associationBlockOfConnector(connector : Connector) {
	Connector.type(connector, type);
	Association(type);
	find sysml.Block(type, _);
}

pattern adjunctPropertyConnectorPropertyInconsistent(property : Property) {
	find sysml.AdjunctProperty(property, _);
	find sysml.ConnectorProperty(property, _);
	find sysml.AdjunctProperty_principal(property, apPrincipal);
	find sysml.ConnectorProperty_connector(property, cpConnector);
	apPrincipal != cpConnector;
}

pattern adjunctPropertyIncompatibleType(property : Property) {
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	find connOrVarOrParam(property);
	find principalType(principal, principalType);
	Property.type(property, type);
	neg find generalClosure(principalType, type);
}

private pattern connOrVarOrParam(element : Element) {
	Connector(element);
} or {
	Variable(element);
} or {
	Parameter(element);
}

private pattern
principalType(principal : Element, principalType : Classifier) {
	Connector(principal);
	Connector.type(principal, principalType);
} or {
	TypedElement.type(principal, principalType);
}

private pattern
generalClosure(classifier : Classifier, general : Classifier) {
	find general *(classifier, general);
}

pattern adjunctPropertyInvalidPrincipalKind(property : Property) {
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	neg Connector(principal);
	neg CallAction(principal);
	neg ObjectNode(principal);
	neg Variable(principal);
	neg Parameter(principal);
	neg InteractionUse(principal);
	neg find submachineState(principal);
}

private pattern submachineState(state : State) {
	State(state);
	State.submachine(state, _);
}

pattern adjunctPropertyNotComposite(property : Property) {
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	Connector(principal);
	neg Property.aggregation(property, ::composite);
} or {
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	CallAction(principal);
	neg Property.aggregation(property, ::composite);
}

pattern adjunctPropertyWrongName(property : Property) {
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	NamedElement.name(principal, name);
	neg Property.name(property, name);
}

pattern blockAssociationNotBinary(association : Association) {
	neg find notAllEndsAreBlocks(association);
	ends == count Association.memberEnd(association, _);
	check(ends != 2);
}

private pattern notAllEndsAreBlocks(association : Association) {
	Association.memberEnd(association, end);
	Property.type(end, class);
	neg find sysml.Block(class, _);
}

pattern blockSpecializationNotBlock(class : Class) {
	find sysml.Block(block, _);
	find general(class, block);
	neg find sysml.Block(class, _);
}

pattern classifierBehaviorPropertyInvalidOwner(property : Property) {
	find sysml.ClassifierBehaviorProperty(property, _);
	neg find validOwner(property);
}

private pattern validOwner(property : Property) {
	find sysml.Block(block, _);
	Class.ownedAttribute(block, property);
	Class.classifierBehavior(block, _);
}

pattern classifierBehaviorPropertyInvalidType(property : Property) {
	find sysml.ClassifierBehaviorProperty(property, _);
	neg find validType(property);
}

private pattern validType(property : Property) {
	Class.ownedAttribute(class, property);
	Class.classifierBehavior(class, clBehavior);
	Property.type(property, type);
	find general *(clBehavior, type);
}

pattern viewNotSingleViewpoint(view : Class) {
	find sysml.View(view, _);
	size == count find viewConforms(view, _);
	check(size != 1);
}

private pattern viewConforms(class : Class, conform : Generalization) {
	find sysml.Conform(conform, _);
	Generalization.specific(conform, class);
}

pattern viewStakeholderNotDerivedFromConform(view : Class) {
	find sysml.View(view, _);
	find sysml.View_stakeholder(view, stakeholder);
	neg find stakeholderDerivedFromConform(view, stakeholder, _);
}

private pattern
stakeholderDerivedFromConform(view : Class, stakeholder : Class, conform : Generalization) {
	find sysml.Conform(conform, _);
	Generalization.specific(conform, view);
	Generalization.general(conform, viewPoint);
	find sysml.Viewpoint_stakeholder(viewPoint, stakeholder);
}

pattern viewViewpointNotDerivedFromConform(view : Class) {
	find sysml.View(view, _);
	find sysml.View_viewPoint(view, viewPoint);
	neg find viewpointDerivedFromConform(view, viewPoint, _);
}

private pattern
viewpointDerivedFromConform(view : Class, viewPoint : Class, conform : Generalization) {
	find sysml.Conform(conform, _);
	Generalization.specific(conform, view);
	Generalization.general(conform, viewPoint);
}

pattern classifierBehaviorPropertyNotComposite(property : Property) {
	find sysml.ClassifierBehaviorProperty(property, _);
	neg Property.aggregation(property, ::composite);
}

pattern requirementNestedClassifierNotRequirement(nested : Class) {
	find sysml.Requirement(requirement, _);
	Class.nestedClassifier(requirement, nested);
	neg find sysml.Requirement(nested, _);
}

pattern
constraintBlockSpecializationNotConstraintBlock(constraintBlock : Class) {
	find sysml.ConstraintBlock(constraintBlock, _);
	Classifier(classifier);
	Classifier.generalization(classifier, generalization);
	Generalization.general(generalization, constraintBlock);
	neg find sysml.ConstraintBlock(classifier, _);
}

pattern distributedPropertyNotBlockOrValueType(property : Property) {
	find sysml.DistributedProperty(property, _);
	neg find propertyOwnedByBlock(property);
	neg find propertyOwnedByValueType(property);
}

private pattern propertyOwnedByBlock(property : Property) {
	find sysml.Block(block, _);
	Class.ownedAttribute(block, property);
}

private pattern propertyOwnedByValueType(property : Property) {
	find sysml.ValueType(valueType, _);
	DataType.ownedAttribute(valueType, property);
}

pattern exposeClientNotView(expose : Dependency) {
	find sysml.Expose(expose, _);
	Dependency.client(expose, client);
	neg find sysml.View(client, _);
}

pattern fullPortBoundToFullPort(port : Port) {
	find sysml.FullPort(port, _);
	find sysml.BindingConnector(connector, _);
	Connector.end(connector, end);
	ConnectorEnd.role(end, port);
	Connector.end(connector, otherEnd);
	ConnectorEnd.role(otherEnd, role);
	role != port;
	find sysml.FullPort(role, _);
}

pattern
invocationOnNestedPortActionOnPortUndefined(action : InvocationAction) {
	find sysml.InvocationOnNestedPortAction(action, _);
	neg InvocationAction.onPort(action, _);
}

pattern itemFlowAssociationHasItemProperty(itemFlow : InformationFlow) {
	find sysml.ItemFlow(itemFlow, _);
	InformationFlow.realization(itemFlow, realization);
	Association(realization);
	find sysml.ItemFlow_itemProperty(itemFlow, _);
}

pattern itemFlowDifferentType(itemFlow : InformationFlow) {
	find sysml.ItemFlow(itemFlow, _);
	find sysml.ItemFlow_itemProperty(itemFlow, itemProperty);
	Property.type(itemProperty, type);
	neg InformationFlow.conveyed(itemFlow, type);
}

pattern itemFlowInvalidType(itemFlow : InformationFlow) {
	find sysml.ItemFlow(itemFlow, _);
	find sysml.ItemFlow_itemProperty(itemFlow, itemProperty);
	Property.type(itemProperty, type);
	neg find sysml.ValueType(type, _);
	neg find sysml.Block(type, _);
	neg Signal(type);
}

pattern itemFlowItemPropertyDifferentName(itemFlow : InformationFlow) {
	find sysml.ItemFlow(itemFlow, _);
	find sysml.ItemFlow_itemProperty(itemFlow, itemProperty);
	InformationFlow.name(itemFlow, itemFlowName);
	Property.name(itemProperty, itemPropertyName);
	check(itemFlowName != itemPropertyName);
}

pattern proxyPortFullPort(port : Port) {
	find sysml.ProxyPort(port, _);
	find sysml.FullPort(port, _);
}

pattern proxyPortSubportNotProxyPort(port : Port) {
	find sysml.ProxyPort(port, _);
	Port.type(port, type);
	Class.ownedAttribute(type, otherPort);
	Port(otherPort);
	neg find sysml.ProxyPort(otherPort, _);
}

pattern proxyPortTypeNotInterfaceBlock(port : Port) {
	find sysml.ProxyPort(port, _);
	Port.type(port, type);
	neg find sysml.InterfaceBlock(type, _);
}

pattern traceNotSingleClient(trace : Abstraction) {
	find sysml.Trace(trace, _);
	size == count Abstraction.client(trace, _);
	check(size != 1);
}

pattern triggerOnNestedPortBaseFullPort(trigger : Trigger) {
	find sysml.TriggerOnNestedPort(trigger, _);
	Trigger.port(trigger, port);
	find sysml.FullPort(port, _);
}

pattern triggerOnNestedPortFullPort(trigger : Trigger) {
	find sysml.TriggerOnNestedPort(trigger, _);
	find sysml.TriggerOnNestedPort_onNestedPort(trigger, port);
	find sysml.FullPort(port, _);
}

pattern triggerOnNestedPortNotSinglePort(trigger : Trigger) {
	find sysml.TriggerOnNestedPort(trigger, _);
	size == count Trigger.port(trigger, _);
	check(size != 1);
}

pattern verifySupplierNotRequirement(verify : Abstraction) {
	find sysml.Verify(verify, _);
	Abstraction.supplier(verify, supplier);
	neg find sysml.AbstractRequirement(supplier, _);
}

pattern acsfeActionOneTrigger(action : AcceptEventAction) {
	find sysml.AcceptChangeStructuralFeatureEventAction(action, _);
	size == count AcceptEventAction.trigger(action, _);
	check(size != 1);
}

pattern acsfeActionInvalidTrigger(action : AcceptEventAction) {
	find sysml.AcceptChangeStructuralFeatureEventAction(action, _);
	AcceptEventAction.trigger(action, trigger);
	neg find triggerEventIsChangeStructuralFeatureEvent(trigger);
}

private pattern
triggerEventIsChangeStructuralFeatureEvent(trigger : Trigger) {
	Trigger.event(trigger, event);
	find sysml.ChangeStructuralFeatureEvent(event, _);
}

pattern acsfeActionNotTwoResultpins(action : AcceptEventAction) {
	find sysml.AcceptChangeStructuralFeatureEventAction(action, _);
	size == count AcceptEventAction.result(action, _);
	check(size != 2);
}

pattern adjunctPropertyMultiplicityRestrictive(property : Property){
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	MultiplicityElement(principal);
	find moreRestrictive(property);
}

private pattern moreRestrictive(property : Property){
	find sysml.AdjunctProperty_principal(property, principal);
	Property.lowerValue(property, propLower);
	MultiplicityElement.lowerValue(principal, princLower);
	LiteralInteger.value(propLower, propLowerValue);
	LiteralInteger.value(princLower, princLowerValue);
	check(propLowerValue > princLowerValue);
} or {
	find sysml.AdjunctProperty_principal(property, principal);
	Property.upperValue(property, propUpper);
	MultiplicityElement.upperValue(principal, princUpper);
	LiteralUnlimitedNatural.value(propUpper, propUpperValue);
	LiteralUnlimitedNatural.value(princUpper, princUpperValue);
	check (propUpperValue >= 0);
	check(
		propUpperValue < princUpperValue ||
		-1 == princUpperValue
	);
}

pattern adjunctPropertySubmachineOrInteraction(property : Property) {
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	find interactionUseOrState(principal);
	neg find correct(property);
}

private pattern interactionUseOrState(element : Element) {
	InteractionUse(element);
} or {
	State(element);
}

private pattern correct(property : Property) {
	find sysml.AdjunctProperty_principal(property, principal);
	neg find principalBehavior(principal, _);
	neg Property.type(property, _);
} or {
	find sysml.AdjunctProperty_principal(property, principal);
	find principalBehavior(principal, behavior);
	Property.type(property, type);
	find general *(behavior, type);
}

private pattern principalBehavior(principal : Element, behavior : Behavior) {
	InteractionUse(principal);
	InteractionUse.refersTo(principal, behavior);
} or {
	State(principal);
	State.submachine(principal, behavior);
}

pattern
allocateActivityPartitionActionsOnClientEnds(activityPartition : ActivityPartition) {
	find sysml.AllocateActivityPartition(activityPartition, _);
	ActivityPartition.node(activityPartition, node);
	Action(node);
	neg find correctActPartNode(activityPartition, node);
}

private pattern
correctActPartNode(activityPartition : ActivityPartition, action : Action) {
	find sysml.Allocate(allocs, _);
	Abstraction.client(allocs, action);
	find representsSupplier(allocs, activityPartition);
}

private pattern
representsSupplier(allocs : Abstraction, activityPartition : ActivityPartition) {
	Abstraction.supplier(allocs, supplier);
	ActivityPartition.represents(activityPartition, supplier);
}

pattern boundReferenceOpposite(boundReference : Property) {
	find sysml.BoundReference(boundReference, _);
	neg find BoundReference_2_opposite_bindingconnector_end(boundReference);
}

private pattern
BoundReference_2_opposite_bindingconnector_end(boundReference : Property) {
	find sysml.BindingConnector(oppositeConnector, _);
	Connector.end(oppositeConnector, opposite);
	ConnectorEnd.role(opposite, role);
	Property.redefinedProperty *(boundReference, role);
	find sysml.BoundReference_boundEnd(boundReference, boundEnd);
	Connector.end(connector, opposite);
	Connector.end(connector, boundEnd);
	boundEnd != opposite;
}

pattern boundReferenceNotReferenceOrValueProperty(boundReference : Property) {
	find sysml.BoundReference(boundReference, _);
	Property.type(boundReference, type);
	neg find sysml.ValueType(type, _);
	Property.aggregation(boundReference, ::composite);
}

pattern boundReferenceNotOrdered(boundReference : Property) {
	find sysml.BoundReference(boundReference, _);
	Property.upperValue(boundReference, upper);
	find greaterThanOne(upper);
	Property.isOrdered(boundReference, false);
}

private pattern greaterThanOne(op : LiteralUnlimitedNatural) {
	LiteralUnlimitedNatural.value(op, value);
	check(value > 1);
} or {
	LiteralUnlimitedNatural.value(op, value);
	check(value < 0);
}

pattern boundReferenceUnique(boundReference : Property) {
	find sysml.BoundReference(boundReference, _);
	Property.upperValue(boundReference, upper);
	find greaterThanOne(upper);
	Property.isUnique(boundReference, true);
}

pattern boundReferenceRedefined(boundReference : Property) {
	find sysml.BoundReference(boundReference, _);
	Property.redefinedProperty(boundReference, redefined);
	find sysml.BoundReference(redefined, _);
}

pattern constraintBlockPropertyNotComposite(constraintBlock : Class) {
	find sysml.ConstraintBlock(constraintBlock, _);
	Class.ownedAttribute(constraintBlock, property);
	neg Property.aggregation(property, ::composite);
}

pattern controlOperatorOperationMethod(method : Behavior) {
	Operation(controlOpertator);
	find sysml.ControlOperator(controlOpertator, _);
	Operation.method(controlOpertator, method);
	neg find sysml.ControlOperator(method, _);
}

pattern deriveReqtSupplierNotReqt(deriveReqt : Abstraction) {
	find sysml.DeriveReqt(deriveReqt, _);
	Abstraction.supplier(deriveReqt, supplier);
	neg find sysml.AbstractRequirement(supplier, _);
}

pattern deriveReqtClientNotReqt(deriveReqt : Abstraction) {
	find sysml.DeriveReqt(deriveReqt, _);
	Abstraction.client(deriveReqt, client);
	neg find sysml.AbstractRequirement(client, _);
}

pattern direcredFeatureInvalidStereotype(feature : Feature) {
	find sysml.DirectedFeature(feature, _);
	neg BehavioralFeature(feature);
	neg find notFlowProperty(feature);
}

private pattern notFlowProperty(feature : Property) {
	Property.redefinedProperty(feature, redefined);
	neg find sysml.FlowProperty(redefined, _);
	Property.subsettedProperty(feature, subsetted);
	neg find sysml.FlowProperty(subsetted, _);
	neg find sysml.FlowProperty(feature, _);
}

pattern optionalLowerNotZero(parameter : Parameter) {
	find sysml.Optional(parameter, _);
	Parameter.lowerValue(parameter, lower);
	LiteralInteger.value(lower, lowerValue);
	check(lowerValue != 0);
}

pattern optionalLowerZero(parameter : Parameter) {
	neg find sysml.Optional(parameter, _);
	Parameter.lowerValue(parameter, lower);
	LiteralInteger.value(lower, lowerValue);
	check(lowerValue == 0);
}

pattern adjunctPropertyIncorrectMultiplicity(property : Property){
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	ObjectNode(principal);
	Property.lowerValue(property, lower);
	LiteralInteger.value(lower, lowerValue);
	check(lowerValue != 0);
} or {
	find sysml.AdjunctProperty(property, _);
	find sysml.AdjunctProperty_principal(property, principal);
	ObjectNode(principal);
	Property.upperValue(property, propertyUpper);
	ObjectNode.upperBound(principal, principalUpper);
	
	LiteralUnlimitedNatural.value(propertyUpper, propertyUpperValue);
	LiteralUnlimitedNatural.value(principalUpper, principalUpperValue);
	check (propertyUpperValue >= 0);
	check(
		principalUpperValue == -1 ||
		propertyUpperValue < principalUpperValue
	);

}

pattern traceNotSingleSupplier(trace : Abstraction) {
	find sysml.Trace(trace, _);
	size == count Abstraction.supplier(trace, _);
	check(size != 1);
}

pattern acsfeActionIncorrectResultpins(action : AcceptEventAction){
	find resultToCheck(action, event, trigger, result);
	neg find correctTypeAndOrdering(action, event, trigger, result);
}

private pattern resultToCheck(action : AcceptEventAction, event : ChangeEvent, trigger: Trigger, result : OutputPin) {
	find sysml.AcceptChangeStructuralFeatureEventAction(action, _);
	find sysml.ChangeStructuralFeatureEvent(event, _);
	AcceptEventAction.trigger(action, trigger);
	Trigger.event(trigger, event);
	AcceptEventAction.result(action, result);
}

private pattern correctTypeAndOrdering(action : AcceptEventAction, event : ChangeEvent, trigger: Trigger, result : OutputPin){
	find resultToCheck(action, event, trigger, result);

	OutputPin.type(result, type);
	find sysml.ChangeStructuralFeatureEvent_structuralFeature(event, feature);
	StructuralFeature.type(feature, type);
	OutputPin.isOrdered(result, ordered);
	StructuralFeature.isOrdered(feature, ordered);
	
	OutputPin.lowerValue(result, rLower);
	StructuralFeature.lowerValue(feature, sfLower);
	LiteralInteger.value(rLower, rLowerValue);
	LiteralInteger.value(sfLower, sfLowerValue);
	check(rLowerValue <= sfLowerValue);
	
	OutputPin.upperValue(result, rUpper);
	StructuralFeature.upperValue(feature, sfUpper);
	LiteralUnlimitedNatural.value(rUpper, rUpperValue);
	LiteralUnlimitedNatural.value(sfUpper, sfUpperValue);
	check((rUpperValue >= sfUpperValue) || (rUpperValue < 0));
}

pattern viewpointNoCreate(viewpoint : Class) {
	find sysml.Viewpoint(viewpoint, _);
	neg find hasCreateOperationamedView(viewpoint);
}

private pattern hasCreateOperationamedView(viewpoint : Class) {
	Class.ownedOperation(viewpoint, operation);
	Operation.name(operation, name);
	check(name == "View");
	find util.stereotypedElement(operation, _, _, "Create");
}

pattern changeStructuralFeatureEventStatic(changeEvent : ChangeEvent) {
	find sysml.ChangeStructuralFeatureEvent(changeEvent, _);
	find
	sysml.ChangeStructuralFeatureEvent_structuralFeature(changeEvent, structuralFeature);
	StructuralFeature.isStatic(structuralFeature, true);
}

pattern
changeStructuralFeatureEventFeatureMultiplicity(changeEvent : ChangeEvent) {
	find sysml.ChangeStructuralFeatureEvent(changeEvent, _);
	size == count find
	sysml.ChangeStructuralFeatureEvent_structuralFeature(changeEvent, _);
	check(size != 1);
}

pattern copyMultipleCopies(copy : Abstraction) {
	find sysml.Copy(copy, _);
	find sysml.AbstractRequirement(req, _);
	Abstraction.client(copy, req);
	Abstraction.client(otherCopy, req);
	copy != otherCopy;
	find sysml.Copy(otherCopy, _);
}

pattern copyCycle(copy : Abstraction) {
	find supplierCopy +(copy, copy);
}

private pattern supplierCopy(copy1 : Abstraction, copy2 : Abstraction) {
	find sysml.Copy(copy1, _);
	find sysml.Copy(copy2, _);
	Abstraction.supplier(copy1, req);
	Abstraction.client(copy2, req);
}

pattern requirementIdNotUnique(req : NamedElement) {
	find sysml.AbstractRequirement(req, _);
	find sysml.AbstractRequirement_Id(req, id);
	find sysml.AbstractRequirement(req2, _);
	find sysml.AbstractRequirement_Id(req2, id);
	req != req2;
}

pattern propertySpecificTypeNotOwnedByBlock(property : Property) {
	Property.type(property, type);
	find sysml.PropertySpecificType(type, _);
	neg find owningBlock(property, _);
}

pattern valuePropertyNotValueType(property : Property) {
	find sysml.ValueProperty(property, _);
	Property.type(property, type);
	neg find sysml.ValueType(type, _);
}

pattern flowPortOwnerNotBlock(port : Port) {
	find sysml.FlowPort(port, _);
	neg find owningBlock(port, _);
}

pattern flowSpecificationInvalidApplication(property : Property) {
	Property.type(property, type);
	find sysml.FlowSpecification(type, _);
	neg find sysml.FlowPort(property, _);
}

pattern flowPortDeprecated(port : Port) {
	find sysml.FlowPort(port, _);
}

pattern flowSpecificationDeprecated(flowSpec : Interface) {
	find sysml.FlowSpecification(flowSpec, _);
}

pattern interfaceBlockInvalidInheritance(interfaceBlock : Class) {
	find sysml.InterfaceBlock(interfaceBlock, _);
	find util.generalizedClassifier(interfaceBlock, general);
	neg find sysml.InterfaceBlock(general, _);
}

pattern partPropertyInvalidType(property : Property) {
	find sysml.PartProperty(property, _);
	Property.type(property, type);
	neg find sysml.Block(type, _);
}

pattern referencePropertyInvalidType(property : Property) {
	find sysml.ReferenceProperty(property, _);
	Property.type(property, type);
	neg find sysml.Block(type, _);
}

pattern sharedPropertyInvalidType(property : Property) {
	find sysml.SharedProperty(property, _);
	Property.type(property, type);
	neg find sysml.Block(type, _);
}

pattern activityStreamingNoNStreaming(activity : Activity) {
	find sysml.streaming(activity, _);
	find sysml.nonStreaming(activity, _);
}